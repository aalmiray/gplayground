/*
 * Copyright (C) 2011-2020 Aestas/IT
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'io.codearte.nexus-staging'

artifacts {
  archives jar
  archives groovydocJar
  archives sourcesJar
  archives distZip
}

release {
  failOnCommitNeeded         = true
  failOnPublishNeeded        = true
  failOnSnapshotDependencies = true
  failOnUnversionedFiles     = true
  failOnUpdateNeeded         = true
  revertOnFail               = true
  preCommitText              = ''
  preTagCommitMessage        = '[Gradle Release Plugin] - pre tag commit: '
  tagCommitMessage           = '[Gradle Release Plugin] - creating tag: '
  newVersionCommitMessage    = '[Gradle Release Plugin] - new version commit: '
  tagTemplate                = 'v${version}'
  buildTasks                 = [ 'build', 'uploadArchives' ]
  versionPropertyFile        = 'gradle.properties'
}


if (!project.version.endsWith('-SNAPSHOT')) {
  apply plugin: 'signing'
  signing {
    sign configurations.archives
  }
}

uploadArchives {
  repositories.mavenDeployer {
    if (project.version.endsWith('-SNAPSHOT')) { 
      repository(url: ossSnapshotRepositoryUrl) {
        authentication(userName: ossSnapshotRepositoryUserName, password: ossSnapshotRepositoryPassword)
      }
    } else {
      repository(url: ossReleaseRepositoryUrl) {
        authentication(userName: ossReleaseRepositoryUserName, password: ossReleaseRepositoryPassword)
      }
      beforeDeployment { deployment -> signing.signPom(deployment) }
    }
    pom.project {
      name pomProjectTitle
      packaging 'jar'
      description pomProjectDescription
      url "https://github.com/aestasit/${githubProjectName}"
      scm {
        url "scm:git@github.com:aestasit/${githubProjectName}.git"
        connection "scm:git@github.com:aestasit/${githubProjectName}.git"
        developerConnection "scm:git@github.com:aestasit/${githubProjectName}.git"
      }
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
      developers {
        developer {
          id 'aadamovich'
          name 'Andrey Adamovich'
        }
        developer {
          id 'lfiandesio'
          name 'Luciano Fiandesio'
        }
      }
    }
  }
}

nexusStaging {
  packageGroup = ossReleaseGroup
}

// TODO: https://developer.github.com/v3/repos/releases/#create-a-release

